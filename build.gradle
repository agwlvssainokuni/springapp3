buildscript {
	ext {
		// リリース物件ビルド構成。
		getProjectProperty = {n, v -> rootProject.hasProperty(n) ? rootProject.getProperty(n) : v }
		propInstid = getProjectProperty("instid", "00")
		propEnvname = getProjectProperty("envname", "prod")
		propTopdir = getProjectProperty("topdir", "/opt/${rootProject.name}")
		propInstdir = getProjectProperty("instdir", "${propTopdir}/${propInstid}")
		propWebdir = getProjectProperty("webdir", "${propInstdir}/web")
		propWebconfdir = getProjectProperty("webconfdir", "${propWebdir}/conf")
		propWeblogdir = getProjectProperty("weblogdir", "${propWebdir}/log")
		propBatchdir = getProjectProperty("batchdir", "${propInstdir}/batch")
		propBatchconfdir = getProjectProperty("batchconfdir", "${propBatchdir}/conf")
		propBatchlogdir = getProjectProperty("batchlogdir", "${propBatchdir}/log")
		// 依存ライブラリのバージョン。
		versionCommonsLang3 = "3.9"
		versionCommonsCollections4 = "4.3"
		versionCommonsCodec = "1.12"
		versionFreemarker = "2.3.28"
		versionSpringBoot = "2.1.6.RELEASE"
		versionSlf4j = "1.7.26"
		versionLogback = "1.2.3"
		versionFluentLogger = "0.3.4"
		versionJunit = "4.12"
		versionHamcrest = "1.3"
		versionMockito = "2.28.2"
	}
	repositories { mavenCentral() }
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${versionSpringBoot}"
	}
}

plugins {
	id "java-library"
	id "eclipse"
}

subprojects {
	apply plugin: "java-library"
	apply plugin: "eclipse"
	apply plugin: "io.spring.dependency-management"
	if (project.name.startsWith("fundamental-")) {
		dependencyManagement {
			imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${versionSpringBoot}") }
		}
	}

	sourceSets {
		delivery
	}

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	compileJava.options.encoding = "UTF-8"
	compileTestJava.options.encoding = "UTF-8"
	compileDeliveryJava.options.encoding = "UTF-8"

	processDeliveryResources {

		def props = new Properties()
		["tokens/basis.properties", "tokens/${propEnvname}.properties", "tokens/${propEnvname}_${propInstid}.properties"].each {
			def file = rootProject.file(it)
			if (file.exists()) {
				file.withInputStream { props.load(it) }
			}
		}

		props.setProperty("tokens.instid", propInstid)
		props.setProperty("tokens.envname", propEnvname)

		props.setProperty("tokens.topdir", propTopdir)
		props.setProperty("tokens.instdir", propInstdir)
		props.setProperty("tokens.webdir", propWebdir)
		props.setProperty("tokens.webconfdir", propWebconfdir)
		props.setProperty("tokens.weblogdir", propWeblogdir)
		props.setProperty("tokens.batchdir", propBatchdir)
		props.setProperty("tokens.batchconfdir", propBatchconfdir)
		props.setProperty("tokens.batchlogdir", propBatchlogdir)

		filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: props)
		rename("\\.template\$", "")
	}

	repositories { mavenCentral() }
	dependencies {
		// elemental
		if (project.name == "elemental-core") {
			implementation "org.apache.commons:commons-lang3:${versionCommonsLang3}"
		}
		if (project.name == "elemental-collection") {
			implementation "org.apache.commons:commons-lang3:${versionCommonsLang3}"
			implementation "org.apache.commons:commons-collections4:${versionCommonsCollections4}"
		}
		if (project.name == "elemental-command") {
			implementation "org.apache.commons:commons-lang3:${versionCommonsLang3}"
		}
		if (project.name == "elemental-crypto") {
			implementation "org.apache.commons:commons-lang3:${versionCommonsLang3}"
			testImplementation "commons-codec:commons-codec:${versionCommonsCodec}"
		}
		if (project.name == "elemental-log") {
			implementation "org.slf4j:slf4j-api:${versionSlf4j}"
		}
		if (project.name == "elemental-paginate") {
			implementation "org.apache.commons:commons-lang3:${versionCommonsLang3}"
		}
		if (project.name == "elemental-wakachi") {
			// なし。
		}
		if (project.name == "elemental-fluent") {
			implementation "org.fluentd:fluent-logger:${versionFluentLogger}"
			implementation "ch.qos.logback:logback-classic:${versionLogback}"
		}

		// fundamental
		if (project.name == "fundamental-appinfo") {
			implementation "org.springframework.boot:spring-boot-starter"
		}
		if (project.name == "fundamental-bizcal") {
			implementation "org.apache.commons:commons-lang3:${versionCommonsLang3}"
			implementation "org.springframework.boot:spring-boot-starter"
		}
		if (project.name == "fundamental-mail") {
			implementation project(":corelib:fundamental:fundamental-bizcal")
			implementation "org.apache.commons:commons-lang3:${versionCommonsLang3}"
			implementation "org.freemarker:freemarker:${versionFreemarker}"
			implementation "org.springframework.boot:spring-boot-starter-jdbc"
			implementation "org.springframework.boot:spring-boot-starter-mail"
		}
		if (project.name == "fundamental-numbering") {
			implementation "org.apache.commons:commons-lang3:${versionCommonsLang3}"
			implementation "org.apache.commons:commons-collections4:${versionCommonsCollections4}"
			implementation "org.springframework.boot:spring-boot-starter"
		}
		// テストライブラリ。
		testImplementation "junit:junit:${versionJunit}"
		testImplementation "org.hamcrest:hamcrest-all:${versionHamcrest}"
		testImplementation "org.mockito:mockito-core:${versionMockito}"
		if (project.name.startsWith("fundamental-")) {
			testImplementation "org.springframework.boot:spring-boot-starter-test"
			testRuntime "org.springframework.boot:spring-boot-starter-aop"
		}
	}
}
